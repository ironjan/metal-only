// use semantic versioning: http://semver.org/
// only change _after_ merge requests
def versionMajor = 0 // incompatible API changes
def versionMinor = 4 // added functionality in a backwards-compatible manner
def versionPatch = 17 // backwards-compatible bug fixes
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://repo1.maven.org/maven2' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.3'        
    }
}

repositories {
    mavenCentral()
    maven { url 'https://android-rome-feed-reader.googlecode.com/svn/maven2/releases' }
}

apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'project-report'
apply plugin: 'com.github.ben-manes.versions'

configurations {
    apt
}


def getSourceSetName(variant) {
    return new File(variant.dirName).getName();
}


android {
    compileSdkVersion 19
    buildToolsVersion '25.0.2'

    packagingOptions {
        // prevents Error: duplicate files during packaging of APK
        // no wildcard support yet
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
    }



    defaultConfig {
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        minSdkVersion 9
        targetSdkVersion 19

        applicationId "com.codingspezis.android.metalonly.player"
    }


    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
        }
    }

    buildTypes {
        debug {
            // buildConfigField "int", "SYNC_INTERVAL", "60" // 1 minute
        }

        release {
            // buildConfigField "int", "SYNC_INTERVAL", "60 * 60 * 12" // 12 hours
        }
    }

    if (project.hasProperty("metalonly.signing")
            && new File(project.property("metalonly.signing") + ".gradle").exists()) {
        apply from: project.property("metalonly.signing") + ".gradle";
    }
}

dependencies {
    compile 'com.android.support:support-v4:22.0.0'
    compile 'com.actionbarsherlock:actionbarsherlock:4.4.0@aar'


    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.3'
    compile 'org.springframework.android:spring-android-rest-template:1.0.1.RELEASE'

    compile 'org.slf4j:slf4j-android:1.7.12'

    apt "org.androidannotations:androidannotations:3.3.2"
    compile "org.androidannotations:androidannotations-api:3.3.2"

    compile fileTree(dir: 'libs', include: '*.jar')

    // tests
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.robolectric:shadows-support-v4:3.0'
    // Repetition for autocompletion in android studio
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.robolectric:shadows-support-v4:3.0'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}
