// use semantic versioning: http://semver.org/
// only change _after_ merge requests
def versionMajor = 0   // incompatible API changes
def versionMinor = 6   // added functionality in a backwards-compatible manner
def versionPatch = 19  // backwards-compatible bug fixes
def versionBuild = 14   // bump for dogfood builds, public betas, etc.

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "project-report"
apply plugin: "com.github.ben-manes.versions"
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android-extensions'

configurations {
    apt
    ktlint
}

android {
    compileSdkVersion project.properties.compileSdkVersion
    buildToolsVersion '28.0.2'

    packagingOptions {
        // prevents Error: duplicate files during packaging of APK
        // no wildcard support yet
        exclude "META-INF/LICENSE"
        exclude "META-INF/NOTICE"
        exclude "META-INF/notice.txt"
        exclude "META-INF/license.txt"
    }

    defaultConfig {
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        if (versionBuild == 0) {
          versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        }
        else {
          versionName "${versionMajor}.${versionMinor}.${versionPatch}-${versionBuild}"
        }

        applicationId "com.codingspezis.android.metalonly.player"
        minSdkVersion project.properties.minSdkVersion
        targetSdkVersion project.properties.targetSdkVersion

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "METAL_ONLY_WUNSCHSCRIPT_POST_URL", "\"https://www.metal-only.de/wunschgruss.html?do=save\""

        buildConfigField "String", "WISH_SUCCESS", "\"Wunsch/Gruss wurde erfolgreich übermittelt\""
        buildConfigField "String", "WISH_FORM_INVALID", "\"Fehler: Bitte Wunsch/Gruss und einen Nick angeben\""
        buildConfigField "String", "WISH_NO_WISHES_POSSIBLE", "\"Wünsche und Grüsse sind nur in der moderierten Sendezeit möglich!\""

        manifestPlaceholders = [fabric_io_id: "b4ef28412703339e2e2ad6f8459433e67a9a64ce"] /* replace this with your own key if necessary */
    }

    sourceSets {
        main {
            manifest.srcFile "AndroidManifest.xml"
            java.srcDirs += "src/main/kotlin"
        }
        test.java.srcDirs += 'src/test/kotlin'
    }

    if (project.hasProperty("metalonly.signing")
            && new File(project.property("metalonly.signing") + ".gradle").exists()) {
        apply from: project.property("metalonly.signing") + ".gradle";
        println("Applied production signing config")
    }
    else{
        println("Project does not have production signing config")
    }
    if (project.hasProperty("metalonly.variants")
            && new File(project.property("metalonly.variants") + ".gradle").exists()) {
        apply from: project.property("metalonly.variants") + ".gradle";
        println("Applied metalonly.variants config")
    }
    else{
        println("Project does not have metalonly.variants config")
    }
    if(project.hasProperty("metalonly.fabric.io")
            && new File(project.property("metalonly.fabric.io") + ".gradle").exists()) {
        apply from: project.property("metalonly.fabric.io") + ".gradle";
        println("Applied fabric.io / crashlytics settings.")
    }else{
        println("Project does not have fabric.io / crashlytics settings.")
    }

    lintOptions {
        disable "InvalidPackage"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
    }

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !travisBuild
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

dependencies {
    implementation project(path: ":core")
    implementation project(path: ":metal-only-client-library")

    implementation "com.android.support:appcompat-v7:$appcompat_v7_version"
    implementation "com.android.support:support-v4:$support_v4_version"
    implementation ('com.crashlytics.sdk.android:crashlytics:2.9.5@aar') {
        transitive = true;
    }
    implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    implementation 'com.hypertrack:hyperlog:0.0.10'
    implementation "org.springframework.android:spring-android-rest-template:$spring_android_rest_template_version"
    implementation "org.slf4j:slf4j-android:$slf4jAndroid_version"
    implementation "org.androidannotations:androidannotations-api:$androidannotations_version"
    implementation "org.androidannotations:rest-spring-api:$androidannotations_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'com.google.android.gms:play-services-auth:16.0.1'


    implementation 'com.google.code.gson:gson:2.8.5'

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.2'

    kapt "org.androidannotations:androidannotations:$androidannotations_version"
    kapt "org.androidannotations:rest-spring:$androidannotations_version"

    implementation "io.arrow-kt:arrow-core:$arrow_version"

    ktlint "com.github.shyiko:ktlint:$ktlint_version"
    
    testImplementation "junit:junit:$junit_version"

    androidTestImplementation("com.android.support.test.espresso:espresso-core:$espresso_version", {
        exclude group: "com.android.support", module: "support-annotations"
    })
    androidTestImplementation("com.android.support.test.espresso:espresso-intents:$espresso_version", {
        exclude group: "com.android.support", module: "support-annotations"
    })
}

kapt {
    correctErrorTypes = true
    arguments {
        arg("resourcePackageName", android.defaultConfig.applicationId)
        arg("androidManifestFile", variant.outputs[0]?.processResources?.manifestFile ?: "")
    }
}

task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}
