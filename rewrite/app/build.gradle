plugins {
	id 'com.android.application'
	id 'kotlin-android'
	id 'kotlin-android-extensions'
	
	id 'com.github.triplet.play' version '2.3.0'
}

// use semantic versioning: http://semver.org/
// only change _after_ merge requests
// only change _after_ merge requests
def versionMajor = 0    // incompatible API changes
def versionMinor = 9    // added functionality in a backwards-compatible manner
def versionPatch = 30   // backwards-compatible bug fixes
def versionBuild = 0    // bump for dogfood builds, public betas, etc.



play {
    if (project.hasProperty("metalonly.publishkey")) {
        serviceAccountCredentials = file(project.property("metalonly.publishkey"))
         println("Added publish key for gradle-play-publish")
    } else {
        println("Disabling publish plugin")
        enabled = false
    }
    defaultToAppBundles = true
}



android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.codingspezis.android.metalonly.player"
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        if (versionBuild == 0) {
          versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        }
        else {
          versionName "${versionMajor}.${versionMinor}.${versionPatch}-${versionBuild}"
        }

        minSdkVersion 19
        targetSdkVersion 28
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    if (project.hasProperty("metalonly.signing")
            && new File(project.property("metalonly.signing") + ".gradle").exists()) {
        apply from: project.property("metalonly.signing") + ".gradle";
        println("Applied production signing config")
    }
    else{
        println("Project does not have production signing config")
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'androidx.media:media:1.1.0-rc01'
    implementation 'com.koushikdutta.ion:ion:2.2.1'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'io.arrow-kt:arrow-core:0.8.2'
    implementation 'com.google.android.gms:play-services-base:17.0.0'
    implementation 'com.android.support:support-media-compat:28.0.0'
    implementation 'com.hypertrack:hyperlog:0.0.10'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
